cmake_minimum_required(VERSION 3.5)

project(KITTI_DATA_LOADER VERSION 0.0.1 LANGUAGES CXX)

find_package(PCL 1.2 REQUIRED COMPONENTS common)

add_library(kitti_loader 
	src/kitti_data_loader.cpp
	include/kitti_data_loader.h  
	include/point_xyzr.h
)

#Add an alias so that library can be used inside the build tree, e.g. when testing
add_library(KITTILoader::DataLoader ALIAS kitti_loader)

#Set target properties
target_include_directories(kitti_loader
    PUBLIC
        $<INSTALL_INTERFACE:include>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}/src
)

target_compile_features(kitti_loader PRIVATE cxx_auto_type)
target_compile_options(kitti_loader PRIVATE $<$<CXX_COMPILER_ID:GNU>:-Wall>)


target_link_libraries(kitti_loader
    PUBLIC
    ${PCL_COMMON_LIBRARIES} KITTILoader::DataLoader
)

##############################################
# Installation instructions

include(GNUInstallDirs)
set(INSTALL_CONFIGDIR ${CMAKE_INSTALL_LIBDIR}/cmake/KITTILoader)

install(TARGETS kitti_loader
	EXPORT KITTILoader-targets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)

#This is required so that the exported target has the name KITTILoader and not kittilaoder
set_target_properties(kitti_loader PROPERTIES EXPORT_NAME KITTILoader)

install(DIRECTORY include/ DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

#Export the targets to a script
install(EXPORT KITTILoader-targets
  FILE
  KITTILoaderTargets.cmake
  NAMESPACE
  KITTILoader::
  DESTINATION
    ${INSTALL_CONFIGDIR}
)

#Create a ConfigVersion.cmake file
include(CMakePackageConfigHelpers)
write_basic_package_version_file(
	${CMAKE_CURRENT_BINARY_DIR}/KITTILoaderConfigVersion.cmake
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

configure_package_config_file(${CMAKE_CURRENT_LIST_DIR}/cmake/KITTILoaderConfig.cmake.in
	${CMAKE_CURRENT_BINARY_DIR}/KITTILoaderConfig.cmake
    INSTALL_DESTINATION ${INSTALL_CONFIGDIR}
)

#Install the config, configversion and custom find modules
install(FILES
	${CMAKE_CURRENT_BINARY_DIR}/KITTILoaderConfig.cmake
	${CMAKE_CURRENT_BINARY_DIR}/KITTILoaderConfigVersion.cmake
    DESTINATION ${INSTALL_CONFIGDIR}
)

##############################################

export(EXPORT KITTILoader-targets FILE ${CMAKE_CURRENT_BINARY_DIR}/KITILoaderTargets.cmake NAMESPACE KITTILoader::)

#Register package in user's package registry
export(PACKAGE KITTILoader)
